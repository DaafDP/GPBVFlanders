*=============================================================================
* File      : GPBVsensitivityCLtreshold.gms
* Author    : David De Pue
* Version   : 1.0
* Date      : 19-January-2017
* Changed   :
* Changed   :
* Remarks:
*Textify parameter declarations in file 'GPBVreportingeconomic.gms'
*(no declarations allowed within loop).
*Calculate optimal 'front' combination of societal profit and total impact on natura 2000 sites
*Solving 'efficiency' and 'effectiveness' scenario for different impact ceilings or profit floors

*===============================================================================
*======Define number of iterations and range profitfloor - impactceiling =======
*===============================================================================

set run /r1*r100/     ;

set scenario /efficiency, effectiveness/ ;

Parameter
pProfitFloor(run)
pImpactCeiling(run) ;

pImpactCeiling(run) = ((ord(run) - 1) * 15) + 500 ;
pProfitFloor(run) = ((ord(run) - 1)*300000) + 65000000 ;

*===============================================================================
*=======================Data preprocessing and data input=======================
*===============================================================================
*Linking source/receptor with right data from 'hoedje'
*Making GDX file with all the data

*Running R code to get calibration set (only when processing new source data)
*$setglobal R "C:\Program Files\R\R-3.3.1\bin\i386\R.exe"
*$setglobal Rfile GPBV.R

*$call "=%R% --rhome=%system.fp% CMD BATCH %rfile% %rfile%.log"

Sets
    sFarm /s1*s826/
    sImpactscores /TIS, SS/
    sCoordinates /X, Y/
    sAnimalCategory
    ssAnimalCategory(sAnimalCategory) ;

$gdxin GPBV.gdx
$load sAnimalCategory
$gdxin

$gdxin BrutoSaldo.gdx
$load ssAnimalCategory
$gdxin

Parameters
pFarmCoord(sFarm, sCoordinates)
pImpactScores(sFarm, sImpactscores)
pFarmAnimals(sFarm, sAnimalCategory)
pPermitYear(sFarm)
pEmissionFactor(sAnimalCategory)
pBrutoSaldo(ssAnimalCategory)
;

$gdxin GPBV.gdx
$load pFarmCoord, pImpactScores, pFarmAnimals, pPermitYear, pEmissionFactor
$gdxin

$gdxin BrutoSaldo.gdx
$load pBrutoSaldo
$gdxin

Scalar pHealthCost health cost (euro) per kg ammonia emitted /36/ ;

Scalar pCLtreshold Critical load treshold /1/ ;

Set sAnimalsIncluded(sAnimalCategory) all animals considered in economic model (dynamic set)
;

sAnimalsIncluded(sAnimalCategory) = yes ;
sAnimalsIncluded('Turkeys') = no ;
sAnimalsIncluded('Horses') = no ;
sAnimalsIncluded('FatteningCalves') = no ;

Set class /Green, Orange, Red/

Parameter
pFarmColour(sFarm) Classify farm according to ANB colour - assuming maximum capacity 1:green 2:orange 3:red
pSS(sFarm) Significance score if full capacity
pTIS(sFarm) Total Impact Score if full capacity
pTotalClassNumbers(class) ;

pSS(sFarm) = (sum(sAnimalsIncluded,(pEmissionFactor(sAnimalsIncluded) * pFarmAnimals(sFarm, sAnimalsIncluded)))/5000) * pImpactScores(sFarm, 'SS') ;
pSS(sFarm)$(pSS(sFarm) = 0) = EPS ;
pTIS(sFarm) = (sum(sAnimalsIncluded,(pEmissionFactor(sAnimalsIncluded) * pFarmAnimals(sFarm, sAnimalsIncluded)))/5000) * pImpactScores(sFarm, 'TIS') ;
pTIS(sFarm)$(pTIS(sFarm) = 0) = EPS ;

Loop(class,
pTotalClassNumbers(class) = 0
) ;

Loop(sFarm,
If (pSS(sFarm) > 50,
   pFarmColour(sFarm) = 3 ;
   pTotalClassNumbers('Red') = pTotalClassNumbers('Red')+1 ;
Elseif pSS(sFarm) <5,
   pFarmColour(sFarm) = 1 ;
   pTotalClassNumbers('Green') = pTotalClassNumbers('Green')+1 ;
Else
   pFarmColour(sFarm) = 2 ;
   pTotalClassNumbers('Orange') = pTotalClassNumbers('Orange')+1 ;  )
   ;
) ;

*===============================================================================
*========Declaration Intermediate parameters + reported parameters==============
*===============================================================================
parameter
dSignificanceScore(sFarm)
dTotalImpactScore(sFarm)
dTotalImpact
dTotalProfit
dClosedFarms
dAmmoniaEmissionFarm(sFarm)
dAmmoniaEmissionRegion
dProfitFarm(sFarm)
dMaxProfitFarm(sFarm)
dMaxProfitRegion
dPercentageofMaxProfit(sFarm)
dPercentageMaxProfitRegion
dMaxImpactRegion
dMaxAmmoniaEmissionRegion
dPrivateProfit
dExternalCostHealth
;

parameter
ImpactCeiling, ProfitFloor, rAmmoniaEmission(scenario), rClosedFarms(scenario) ;

parameters
zClosedFarms(run, scenario)
zPercentageMaxProfit(run, scenario)
zTotalImpact(run, scenario)
zTotalProfit(run, scenario)
zPrivateProfit(run, scenario)
zExternalHealthCost(run, scenario)
zModelStat(run, scenario)
zSolveStat(run, scenario)
zPercentageMaxProfitFarm(run, scenario, sFarm)
;

*===============================================================================
*========================Model (all equations-all scenarios)====================
*===============================================================================

Variables
vAmmoniaEmissionRegion
vAmmoniaEmissionFarm(sFarm)
vProfitSociety
vProfitFarm(sFarm) ;

Positive  variable
vAnimals(sFarm, sAnimalCategory) ;

Equations
eqAnimals(sFarm, sAnimalCategory) Permit constraint
eqAmmoniaEmissionFarm(sFarm) ammonia emission per farm
eqAmmoniaEmissionRegion total ammonia emission region
eqProfitFarm(sFarm) profit per farm
eqProfitSociety objective fucntion
eqYoungCows(sFarm) For every adult cow we assume there's one cow younger than 1 and 1 cow 1 to 2
eqCows(sFarm)  For every adult cow we assume there's one cow younger than 1 and 1 cow 1 to 2
;

eqAmmoniaEmissionFarm(sFarm)..
vAmmoniaEmissionFarm(sFarm) =e= sum(sAnimalsIncluded, (pEmissionFactor(sAnimalsIncluded) * vAnimals(sFarm, sAnimalsIncluded))) ;

eqAnimals(sFarm, sAnimalCategory)..
vAnimals(sFarm, sAnimalCategory) =l= pFarmAnimals(sFarm, sAnimalCategory) ;

eqAmmoniaEmissionRegion..
vAmmoniaEmissionRegion =e= SUM(sFarm,vAmmoniaEmissionFarm(sFarm))    ;

eqProfitFarm(sFarm)..
vProfitFarm(sFarm) =e= sum(ssAnimalCategory, (vAnimals(sFarm, ssAnimalCategory) * pBrutoSaldo(ssAnimalCategory)))   ;

eqProfitSociety..
vProfitSociety =e= sum(sFarm, vProfitFarm(sFarm)) - (pHealthCost * vAmmoniaEmissionRegion) ;

eqYoungCows(sFarm)..
vAnimals(sFarm, 'Cows0to1') - vAnimals(sFarm, 'Cows1to2') =e= 0 ;

eqCows(sFarm)..
vAnimals(sFarm, 'AdultCows') - 3*vAnimals(sFarm, 'Cows0to1')  =e= 0 ;

*-------------------------------------------------------------------------------
*Scen1: efficiency check: Total Impact max. sc1, max. profit society, no individual farm constraints
*-------------------------------------------------------------------------------

Equations
eqTotalImpactRegion
;

*lower than total impact from previous model
eqTotalImpactRegion..
sum(sFarm, (vAmmoniaEmissionFarm(sFarm)/5000)* pImpactScores(sFarm, 'TIS')) =l= ImpactCeiling ;

Model efficiency /eqAnimals, eqAmmoniaEmissionFarm, eqAmmoniaEmissionRegion, eqProfitFarm, eqProfitSociety, eqYoungCows, eqCows, eqTotalImpactRegion/          ;

*-------------------------------------------------------------------------------
**Scen2: Effectivity check, minimize TIS,  societal profit bigger  than sc1
*-------------------------------------------------------------------------------

Variable
vTotalImpact
;

Equation
eqTotalImpactRegionSc7
eqSocietalProfit ;

eqTotalImpactRegionSc7..
vTotalImpact =e= sum(sFarm, (vAmmoniaEmissionFarm(sFarm)/5000)* pImpactScores(sFarm, 'TIS')) ;

eqSocietalProfit..
vProfitSociety =g= ProfitFloor ;

Model effectiveness /eqAnimals, eqAmmoniaEmissionFarm, eqAmmoniaEmissionRegion, eqProfitFarm, eqProfitSociety, eqYoungCows, eqCows,
eqTotalImpactRegionSc7 + eqSocietalProfit/ ;

*===============================================================================
*=======================Different model runs====================================
*===============================================================================
loop(run,

ImpactCeiling = pImpactCeiling(run)          ;
ProfitFloor = pProfitFloor(run)              ;

Solve efficiency maximizing vProfitSociety using lp ;

$batinclude GPBVreportingeconomic.gms

zClosedFarms(run, 'efficiency') = dClosedFarms ;
zPercentageMaxProfit(run, 'efficiency') =  dPercentageMaxProfitRegion ;
zTotalImpact(run, 'efficiency') = dTotalImpact ;
zTotalProfit(run, 'efficiency') = dTotalProfit ;
zPrivateProfit(run, 'efficiency') = dPrivateProfit  ;
zExternalHealthCost(run, 'efficiency') = dExternalCostHealth ;
zModelStat(run, 'efficiency') = efficiency.MODELSTAT  ;
zSolveStat(run, 'efficiency') = efficiency.SOLVESTAT  ;
zPercentageMaxProfitFarm(run, 'efficiency', sFarm) = dPercentageofMaxProfit(sFarm)  ;

Solve effectiveness using lp minimizing vTotalImpact ;

$batinclude GPBVreportingeconomic.gms

zClosedFarms(run, 'effectiveness') = dClosedFarms ;
zPercentageMaxProfit(run, 'effectiveness') =  dPercentageMaxProfitRegion ;
zTotalImpact(run, 'effectiveness') = dTotalImpact ;
zTotalProfit(run, 'effectiveness') = dTotalProfit ;
zPrivateProfit(run, 'effectiveness') = dPrivateProfit  ;
zExternalHealthCost(run, 'effectiveness') = dExternalCostHealth ;
zModelStat(run, 'effectiveness') = effectiveness.MODELSTAT  ;
zSolveStat(run, 'effectiveness') = effectiveness.SOLVESTAT  ;
zPercentageMaxProfitFarm(run, 'effectiveness', sFarm) = dPercentageofMaxProfit(sFarm)  ;


);

*===============================================================================
*=======================Write away results======================================
*===============================================================================
execute_unloaddi 'GPBVOptimalProfitvsImpact.gdx'
